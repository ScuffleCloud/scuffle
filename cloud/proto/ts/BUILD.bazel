load("@aspect_bazel_lib//lib:output_files.bzl", "output_files")
load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//cloud/proto/ts:@swc/cli/package_json.bzl", swc_cli_bin = "bin")
load("@npm//cloud/proto/ts:typescript/package_json.bzl", typescript_bin = "bin")
load("//misc/utils/protobuf/ts:defs.bzl", "protobuf_ts_compile")

npm_link_all_packages()

# Generate the typescript protobuf code.
protobuf_ts_compile(
    name = "proto",
    protos = [
        "@googleapis//google/rpc:status_proto",
        "@googleapis//google/rpc:error_details_proto",
        "//cloud/proto:pb",
    ],
)

# Compile that using tsc to get ESM + Types
typescript_bin.tsc(
    name = "types",
    srcs = [
        ":node_modules",
        ":package.json",
        ":proto",
        ":tsconfig.json",
    ],
    args = [
        "-p",
        "tsconfig.json",
        "--emitDeclarationOnly",
        "--outDir",
        "dist/types",
    ],
    chdir = package_name(),
    out_dirs = ["dist/types"],
)

swc_cli_bin.swc(
    name = "esm",
    srcs = [
        ":.swcrc",
        ":node_modules",
        ":package.json",
        ":proto",
    ],
    args = [
        "./proto",
        "-d",
        "./dist/esm",
        "--strip-leading-paths",
    ],
    chdir = package_name(),
    out_dirs = ["dist/esm"],
)

# Since we dont know the outputs ahead of time
# We are using directory output mode, so we need to select
# that `generated` directory.
# This directory is the same one referenced in the `package.json` exports section.
output_files(
    name = "types_output",
    paths = [package_name() + "/dist/types"],
    target = ":types",
)

output_files(
    name = "esm_output",
    paths = [package_name() + "/dist/esm"],
    target = ":esm",
)

# This allows it to be referenced by other projects such as `cloud/dashboard`
# Unfortunately the name `pkg` is special. So it must be named that.
npm_package(
    name = "pkg",
    srcs = [
        "package.json",
        ":esm_output",
        ":types_output",
    ],
    visibility = ["//visibility:public"],
)
