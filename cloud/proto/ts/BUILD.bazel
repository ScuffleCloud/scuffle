load("@aspect_bazel_lib//lib:output_files.bzl", "output_files")
load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//cloud/proto/ts:typescript/package_json.bzl", typescript_bin = "bin")
load("//misc/utils/protobuf/ts:defs.bzl", "protobuf_ts_compile")

npm_link_all_packages()

# Generate the typescript protobuf code.
protobuf_ts_compile(
    name = "proto",
    protos = ["//cloud/proto:pb"],
)

# Compile that using tsc to get ESM + Types
typescript_bin.tsc(
    name = "ts_lib",
    srcs = [
        ":node_modules",
        ":package.json",
        ":proto",
        ":tsconfig.json",
    ],
    args = [
        "-p",
        "tsconfig.json",
        "--outDir",
        "generated",
    ],
    chdir = package_name(),
    out_dirs = ["generated"],
    visibility = ["//visibility:public"],
)

# Since we dont know the outputs ahead of time
# We are using directory output mode, so we need to select
# that `generated` directory.
# This directory is the same one referenced in the `package.json` exports section.
output_files(
    name = "ts_lib_copy",
    paths = [package_name() + "/generated"],
    target = ":ts_lib",
)

# This allows it to be referenced by other projects such as `cloud/dashboard`
# Unfortunately the name `pkg` is special. So it must be named that.
npm_package(
    name = "pkg",
    srcs = [
        "package.json",
        ":ts_lib_copy",
    ],
    visibility = ["//visibility:public"],
)
