load("@bazel_skylib//lib:selects.bzl", "selects")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")
load("//misc/utils:binary_wrapper.bzl", "binary_wrapper")

alias(
    name = "libexec",
    actual = select({
        "//misc/platforms:linux-x86_64": "@valgrind-linux-x86_64//:libexec",
        "//misc/platforms:linux-aarch64": "@valgrind-linux-aarch64//:libexec",
    }),
    tags = [
        "manual",
        "no-cache",
    ],
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

binary_wrapper(
    name = "valgrind_bin",
    binary = select({
        "//misc/platforms:linux-x86_64": "@valgrind-linux-x86_64//:valgrind",
        "//misc/platforms:linux-aarch64": "@valgrind-linux-aarch64//:valgrind",
    }),
    data = [":libexec"],
    env = {
        "VALGRIND_LIB": "$$RUNFILES/$(rlocationpath :libexec)/valgrind",
    },
    extra_commands = [
        "ulimit -n 4096",
    ],
    tags = [
        "manual",
        "no-cache",
    ],
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

sh_binary(
    name = "valgrind_dummy",
    srcs = ["dummy.sh"],
)

alias(
    name = "valgrind",
    actual = selects.with_or({
        ("//misc/platforms:linux-x86_64", "//misc/platforms:linux-aarch64"): ":valgrind_bin",
        "//conditions:default": ":valgrind_dummy",
    }),
    visibility = ["//visibility:public"],
)
